import io.gitlab.arturbosch.detekt.Detekt
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'application'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.spongepowered.configurate.build.component'
    id 'io.gitlab.arturbosch.detekt'
    id 'org.jlleitschuh.gradle.ktlint'
}

description = "CLI tool to inspect Configurate's view of files"

dependencies {
    // Configurate
    implementation projects.format.xml
    implementation projects.format.yaml
    implementation projects.format.gson
    implementation projects.format.hocon
    implementation projects.extra.extraKotlin

    // Libraries
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "com.github.ajalt:clikt:2.8.0"
    implementation "org.fusesource.jansi:jansi:2.4.0"
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions.freeCompilerArgs = ["-Xnew-inference"]
}

tasks.jar.configure {
    manifest.attributes "Specification-Version": project.version,
         "Implementation-Version": project.version
}

tasks.withType(Detekt).configureEach {
    config.setFrom(rootProject.file(".detekt/detekt.yml"))
}

application {
    mainClass = "org.spongepowered.configurate.tool.ToolKt"
}
